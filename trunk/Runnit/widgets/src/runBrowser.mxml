<?xml version="1.0" encoding="utf-8"?>
<mx:Application xmlns:mx="http://www.adobe.com/2006/mxml" layout="absolute" xmlns:maps="com.google.maps.*">
	<mx:Script>
		<![CDATA[
			import com.vizzuality.gmaps.RunSingleMarker;
			import mx.rpc.events.ResultEvent;
			import com.vizzuality.gmaps.RunMarkerCluster;
			import com.google.maps.overlays.Marker;
			import com.kelvinluck.gmaps.Clusterer;
			import com.google.maps.MapZoomEvent;
			import com.google.maps.LatLng;
			import com.google.maps.controls.ZoomControl;
			
			private var clusterer:Clusterer;
			private var markers:Array;
			private var attachedMarkers:Array;			
			
			private function preinit():void {
				
			}
			private function init():void {
				map.addControl(new ZoomControl());
				map.setCenter(new LatLng(51.32, 0), 3);
				map.enableScrollWheelZoom();
				map.addEventListener(MapZoomEvent.ZOOM_CHANGED, onMapZoomChanged);
				server.getAllRuns();
				
			}
			
			private function onMarkersResult(event:ResultEvent):void
			{
				
				markers = [];	
				for each(var m:Object in event.result as Array) {
					markers.push(new RunSingleMarker(new LatLng(m.lat,m.lon)));
				}
				
				
				clusterer = new Clusterer(markers, map.getZoom(), 30);
				attachedMarkers = [];
				attachMarkers();					
				
			}	
			
			private function onMapZoomChanged(event:MapZoomEvent):void
			{
				clusterer.zoom = map.getZoom();
				attachMarkers();
			}
	 
			private function attachMarkers():void
			{
				for each (var marker:Marker in attachedMarkers) {
					map.removeOverlay(marker);
				}
				attachedMarkers = [];
				var clusteredMarkers:Array = clusterer.clusters;
	 
				for each (var cluster:Array in clusteredMarkers) {
					if (cluster.length == 1) {
						// there is only a single marker in this cluster
						marker = cluster[0];
					} else {
						marker = new RunMarkerCluster(cluster);
					}
					map.addOverlay(marker);
					attachedMarkers.push(marker);
				}
			}
					
			
		]]>
	</mx:Script>


	<maps:Map id="map" key="jhkjhjh" mapevent_mappreinitialize="preinit()" mapevent_mapready="init()" width="100%" height="100%" />
	<mx:RemoteObject id="server" endpoint="http://localhost/amfphp/gateway.php" destination="RunnitServices" source="RunnitServices"
		 result="onMarkersResult(event)" showBusyCursor="false" />
</mx:Application>
