<?xml version="1.0" encoding="utf-8"?>
<mx:Application xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:maps="com.google.maps.*"
	 layout="absolute" width="721" height="423" color="#747474" backgroundGradientAlphas="[1.0, 1.0]"
	 backgroundGradientColors="[#EAF7FA, #E6F7F7]" currentState="loading">
	<mx:states>
		<mx:State name="loading">
			<mx:SetProperty target="{addressInput}" name="enabled" value="false"/>
			<mx:SetProperty target="{button1}" name="enabled" value="false"/>
			<mx:AddChild position="lastChild">
				<mx:Canvas backgroundColor="#000000" backgroundAlpha="0.7" left="10" right="10" top="80" bottom="23">
					<mx:Label text="Cargando" horizontalCenter="-1" verticalCenter="-30" color="#FFFFFF" fontSize="17" fontFamily="DINOT-Medium"/>
					<mx:Text text="Estamos conectando con nuestro servicio de geoposicionamiento,&#xd;Por favor espere unos instantes...&#xd;&#xd;" verticalCenter="19" color="#FFFFFF" fontSize="14" fontFamily="DINOT-Medium" width="490" x="105" textAlign="center"/>
				</mx:Canvas>
			</mx:AddChild>
		</mx:State>
	</mx:states>
<mx:Script>
	<![CDATA[
		import com.google.maps.MapType;
		import com.adobe.serialization.json.JSON;
		import com.google.maps.overlays.MarkerOptions;
		import com.google.maps.controls.MapTypeControlOptions;
		import com.google.maps.controls.MapTypeControl;
		import com.google.maps.LatLngBounds;
		import mx.rpc.events.FaultEvent;
		import mx.rpc.events.ResultEvent;
		import mx.rpc.http.mxml.HTTPService;
		import mx.controls.Alert;
		import com.google.maps.overlays.Marker;
		import com.google.maps.services.Placemark;
		import com.google.maps.services.GeocodingResponse;
		import com.google.maps.services.GeocodingEvent;
		import com.google.maps.services.ClientGeocoderOptions;
		import com.google.maps.services.ClientGeocoder;
		import com.google.maps.controls.ControlPosition;
		import com.google.maps.controls.ZoomControlOptions;
		import com.google.maps.controls.ZoomControl;
		import com.google.maps.LatLng;
		import com.google.maps.MapEvent;
		
		private var geocoder:ClientGeocoder;
		private var sanecans:Array=new Array();
		
		private var positionMarker:Marker;
		
	
		private function onMapReady(event:MapEvent):void {
			//map.setCenter(new LatLng(40.3228,-3.8655),13);
			var zco:ZoomControlOptions=new ZoomControlOptions({
				position: new ControlPosition(ControlPosition.ANCHOR_TOP_LEFT, 12, 12)
			});
			var zc:ZoomControl = new ZoomControl(zco);
			map.addControl(zc);
			
/* 			var mtco:MapTypeControlOptions=new MapTypeControlOptions({
				position: new ControlPosition(ControlPosition.ANCHOR_TOP_LEFT, 32, 12)
			});
			var mtc:MapTypeControl = new MapTypeControl(mtco);
			map.addControl(mtc); */
			
			
			geocoder = new ClientGeocoder(
			    new ClientGeocoderOptions({
			      language: "es",
			      viewport: map.getLatLngBounds()
			    }));
			
			geocoder.addEventListener(GeocodingEvent.GEOCODING_SUCCESS,onGeocodeResponse);    
			geocoder.addEventListener(GeocodingEvent.GEOCODING_FAILURE,onGeocodeFault);    
		
		
			var srv:HTTPService =  new HTTPService();
			srv.url = "http://pipes.yahooapis.com/pipes/pipe.run?_id=bLTo_nJE3hGRRTLFggSecQ&_render=json";
			srv.addEventListener(ResultEvent.RESULT,onSrvResultJSON);
			srv.addEventListener(FaultEvent.FAULT,onSrvFault);
			srv.send();
		}
		
		private function onSrvResultJSON(event:ResultEvent):void {
			var bbox:LatLngBounds=new LatLngBounds();
			var jsonObj:Object =  JSON.decode(event.result as String);
			for each (var item:Object in jsonObj.value.items as Array) {
			      //create markers
			      var latlng:LatLng=new LatLng(item["latitude"],item["longitude"]);
				  var options:MarkerOptions = new MarkerOptions({
							icon: new CustomIconSprite(item["type"]),
							iconAlignment: MarkerOptions.ALIGN_BOTTOM,
							iconOffset: new Point(-11,0),
							clickable:false
							});			      
			      
			      
			      var m:Marker = new Marker(latlng,options);
			      bbox.extend(latlng);
			      map.addOverlay(m);					
			}
			
  			map.setCenter(bbox.getCenter(),map.getBoundsZoomLevel(bbox));	
  			currentState='';			
		}
		
		
		private function onSrvResultCSV(event:ResultEvent):void {
			var bbox:LatLngBounds=new LatLngBounds();
			
			var result:String=(event.result as String);
			var properties:Array = new Array();
			var headings:Boolean = false;
			var carriage:Number = NaN;
			var comma:Number = NaN;
			var cursor:Number = 0;
			var sub:Number = NaN;
			var item:Object = null;
			var value:String = null;
			var line:String = null;			
			while(result.indexOf( "\n", cursor ) != -1 ) {
				carriage = result.indexOf( "\n", cursor );
				line = result.substring( cursor, carriage );
				line=line+",";
				cursor = 0;
				sub = 0;		
				item = new Object();		
				while( line.indexOf( ",", cursor ) != -1 ) {
				  comma = line.indexOf( ",", cursor );
				  value = line.substring( cursor, comma );
				
				  if( !headings ) {
				    properties.push( value );
				  } else {
				    item[properties[sub]] = value;
				  }				
				  cursor = comma + 1;
				  sub++;
				} 		
			    value = line.substring( cursor, line.length - 1 );
			
			    if( !headings ) {
			      properties.push( value );
			      headings = true;
			    } else {
			      item[properties[sub]] = value;
			      sanecans.push( item );
			      
			      trace(item["latitude"] +","+item["longitude"]);
			      //create markers
			      var latlng:LatLng=new LatLng(item["latitude"],item["longitude"]);
				  var options:MarkerOptions = new MarkerOptions({
							icon: new CustomIconSprite(item["type"]),
							iconAlignment: MarkerOptions.ALIGN_BOTTOM,
							iconOffset: new Point(-11,0),
							clickable:false
							});			      
			      
			      
			      var m:Marker = new Marker(latlng,options);
			      bbox.extend(latlng);
			      map.addOverlay(m);			      		      
			      
			    }		
				cursor = carriage + 1;
  			}	
  			
  			map.setCenter(bbox.getCenter(),map.getBoundsZoomLevel(bbox));	
  			currentState='';
  			    		
		}
		private function onSrvFault(event:FaultEvent):void {
			trace(event.fault);
		}
		
		
		private function onAddressClick():void {
			var address:String = addressInput.text;
			if (address.toLowerCase().indexOf("mostoles")<1)
				address+=",mostoles";
			if (address.toLowerCase().indexOf("españa")<1)
				address+=",españa";
			geocoder.geocode(address);
			if(positionMarker!=null)
				map.removeOverlay(positionMarker);
		}
		
		private function onGeocodeResponse(event:GeocodingEvent):void {
			var result:GeocodingResponse = event.response as GeocodingResponse;
			var pm:Placemark = result.placemarks[0] as Placemark;
	 		var pointCoords:LatLng=new LatLng(pm.point.lat(),pm.point.lng());
			  var options:MarkerOptions = new MarkerOptions({
						icon: new CustomIconSprite("position"),
						iconAlignment: MarkerOptions.ALIGN_BOTTOM,
						iconOffset: new Point(-11,0),
						clickable:false
						});			 		
	 		
			positionMarker = new Marker(pm.point,options);
			map.addOverlay(positionMarker);
			map.setCenter(pm.point,16);
			
		}
		
		private function onGeocodeFault(event:GeocodingEvent):void {
			Alert.show("Disculpa, no hemos encontrado la dirección que has introducido");
		}
		
		private var alreadyCleaned:Boolean=false;
		private function clearInput():void {
			if (!alreadyCleaned) {
				addressInput.text="";
				alreadyCleaned=true;
			}
		}
		
		private function checkCurrentMapType(event:MouseEvent):void {
			sat.selected=false;
			normal.selected=false;
			hybrid.selected=false;
			(event.currentTarget as Button).selected=true;
		}
		
	]]>
</mx:Script>

	<mx:Style source="com/vizzuality/skin/fonts.css"/>
	<mx:Style source="com/vizzuality/skin/ui.css"/>

	<mx:Canvas backgroundColor="#eaf7fa" width="100%" height="100%" id="canvas1">
		<mx:Canvas x="409" y="409" width="5" height="5" backgroundColor="#DB1B29">
		</mx:Canvas>
		<mx:Canvas x="287" y="409" width="5" height="5" backgroundColor="#FF9000">
		</mx:Canvas>
		<mx:Label x="295" y="403" text="Dirección introducida" fontFamily="DINOT-Medium" color="#666666"/>
		<mx:Label x="417" y="403" text="Clínicas adheridas" fontFamily="DINOT-Medium" color="#666666"/>
		<mx:Canvas x="519" y="409" width="5" height="5" backgroundColor="#3F9042">
		</mx:Canvas>
		<mx:Canvas x="618" y="409" width="5" height="5" backgroundColor="#00A3DB">
		</mx:Canvas>
		<mx:Label x="626" y="403" text="Tiendas adheridas" fontFamily="DINOT-Medium" color="#666666"/>
		<mx:Label x="527" y="403" text="Puntos Sanecan" fontFamily="DINOT-Medium" color="#666666"/>
	</mx:Canvas>
	<mx:Canvas backgroundColor="#999999" left="9" right="9" top="79" bottom="22"/>
	<mx:TextInput id="addressInput" left="10" right="93" enter="onAddressClick()" 
		text="Introduce tu dirección" click="clearInput()" fontFamily="Arial" fontSize="15" 
		borderStyle="solid" cornerRadius="3" color="#A9A9A9" borderColor="#D3D3D3" height="26" top="46"
		focusAlpha="0" paddingLeft="5"/>
	<mx:Button label="Buscar" right="10" id="button1" click="onAddressClick()" styleName="btnBuscar" 
		fontFamily="DINOT-Medium" height="26" top="46"
		mouseChildren="false" useHandCursor="true" buttonMode="true"/>
	<maps:Map id="map" left="10" right="10" top="80" key="ABQIAAAAtDJGVn6RztUmxjnX5hMzjRQ90078BGwgMQ1AXXdPNoLNuCNk4hQ4ed9_s377T8EfU8iTpdFjsz8dgA" 
		mapevent_mapready="onMapReady(event)" bottom="23"/>	
	<mx:Button id="sat" x="502" y="90" label="Satélite" click="map.setMapType(MapType.SATELLITE_MAP_TYPE);checkCurrentMapType(event)" styleName="btnBuscar" height="24" fontSize="12"/>
	<mx:Button id="normal" x="571" y="90" label="Mapa" click="map.setMapType(MapType.NORMAL_MAP_TYPE);checkCurrentMapType(event)" selected="true" styleName="btnBuscar" height="24" fontSize="12"/>
	<mx:Button id="hybrid" x="635" y="90" label="Híbrido" click="map.setMapType(MapType.HYBRID_MAP_TYPE);checkCurrentMapType(event)" styleName="btnBuscar" height="24" fontSize="12"/>
	<mx:HBox y="8" width="701" id="hbox1" left="9" horizontalGap="-6">
		<mx:Label text="Localiza los sanecan, " fontSize="22" fontFamily="DINOT-Bold" id="label0" color="#346376"/>
		<mx:Label text="tiendas y clínicas mas cercanas a tí" fontSize="22" fontFamily="DINOT-Bold" id="label1" color="#FF9900"/>
	</mx:HBox>
	<mx:HRule x="0" strokeWidth="1" strokeColor="#C0E7F0" width="100%" top="0"/>
<!--	<maps:Map id="map" height="326" left="2" right="2" top="72" key="ABQIAAAAtDJGVn6RztUmxjnX5hMzjRTJgdjRiA1iEZLNaLDwBpXQC8fT5xQjnAIzzs7GWWt3bGiUN4mI-fpuBg" 
		mapevent_mapready="onMapReady(event)"/>	-->
</mx:Application>
