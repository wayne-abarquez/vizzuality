<?xml version="1.0" encoding="utf-8"?>
<mx:VBox xmlns:mx="http://www.adobe.com/2006/mxml" width="100%" height="100%" xmlns:maps="com.google.maps.*" addedToStage="added()"  creationComplete="init()"
	xmlns:components="org.vizzuality.components.*">
	<mx:states>
		<mx:State name="loading" enterState="loadingEnter()" exitState="loadingExist()">
			<mx:AddChild relativeTo="{canvas1}" position="lastChild">
				<mx:Canvas x="0" y="0" width="100%" height="100%" backgroundColor="#FFFFFF">
					<mx:SWFLoader horizontalCenter="0" verticalCenter="0" source="{Resource.LOAD_ANIMATION}" 
						alpha=".6"/>
				</mx:Canvas>
			</mx:AddChild>
		</mx:State>
		<mx:State name="list"/>
	</mx:states>
	
<mx:Style>
	.selectionRectangleStyle {
	backgroundAlpha: 0.3;
	backgroundColor: #067EE3;
	borderColor: #FFFFFF;
	borderStyle: inset;
	borderThickness: 2;
	cornerRadius: 0;
	}
</mx:Style>

<mx:Script>
	<![CDATA[
		import org.vizzuality.events.MyEventDispatcher;
		import org.vizzuality.events.DataSelectionEvent;
		import com.google.maps.MapMoveEvent;
		import org.vizzuality.model.Pa;
		import com.adobe.serialization.json.JSON;
		import com.google.maps.MapAction;
		import mx.rpc.events.ResultEvent;
		import com.google.maps.overlays.Marker;
		import com.google.maps.services.GeocodingEvent;
		import com.google.maps.services.ClientGeocoder;
		import com.google.maps.interfaces.IPane;
		import org.vizzuality.maps.GeoserverTileLayer;
		import com.google.maps.overlays.TileLayerOverlay;
		import com.google.maps.controls.MapTypeControl;
		import com.asual.swfaddress.SWFAddress;
		import mx.managers.CursorManager;
		import com.google.maps.MapZoomEvent;
		import mx.events.ScrollEvent;
		import mx.core.Application;
		import com.google.maps.LatLngBounds;
		import com.google.maps.MapMouseEvent;
		import com.google.maps.controls.ControlPosition;
		import com.google.maps.controls.ZoomControlOptions;
		import com.google.maps.MapType;
		import com.google.maps.LatLng;
		import com.google.maps.controls.ZoomControl;
		import com.google.maps.MapEvent;
		import resource.Resource;
		import mx.collections.ArrayCollection;
		
		
		private var aSprite:Sprite;
		private var bSprite:Sprite;
		
		private var selectedArea: ArrayCollection = new ArrayCollection();
		private var dataBbox:LatLngBounds;	
		private var selectionRectangle:Canvas;
		private var r:Rectangle;
		private var scrollPosition: Number = 0;
		
		private	var tl:GeoserverTileLayer;
		private	var tlo:TileLayerOverlay;			
		private var tilesPane:IPane;
		
		private var filter:ColorMatrixFilter = new ColorMatrixFilter([0.13238779460000002,1.0025112879999998,0.0490316186,0,-43.75271063919999,0.0749877946,1.230711288,0.0490316186,0,-78.25571063919999,0.0749877946,1.0025112879999998,0.15963161860000002,0,-56.89591063919999,0,0,0,1,0]);
		private var emptyFilter:ColorMatrixFilter = new ColorMatrixFilter();		
	
		private var totalAreas:Number=0;
		[Bindable] private var page:Number=1;
		[Bindable] private var totalPages:Number=1;

	
        [Bindable] private var areasArr: ArrayCollection = new ArrayCollection();	
		
		private function loadingEnter():void {
			if (map!=null && map.isLoaded()) {
				map.disableDragging();
				map.disableScrollWheelZoom();
				map.setDoubleClickMode(MapAction.ACTION_NOTHING);
				if (bSprite==null) {
					aSprite = map.getChildAt(1) as Sprite;
					bSprite = aSprite.getChildAt(0) as Sprite;
				}
				bSprite.filters = [filter];
			}
			
		}
	
		private function loadingExist():void {
			if (map!=null && map.isLoaded() &&bSprite!=null) {
				map.enableDragging();
				map.enableScrollWheelZoom();
				map.setDoubleClickMode(MapAction.ACTION_ZOOM_IN);
				bSprite.filters = [emptyFilter];
			}
			
		}
	
		private function init():void {
		    selectionRectangle = new Canvas();
			selectionRectangle.setStyle("styleName", "selectionRectangleStyle");
			Application.application.addEventListener(ScrollEvent.SCROLL,onScrollApplication);
			r = new Rectangle(0,0,0,0);
			getPas(1);
			
			MyEventDispatcher.addEventListener(DataSelectionEvent.PA_REMOVED,onPaRemoved);
			
		}
		
		private function onPaRemoved(event:DataSelectionEvent):void {
			event.selectedPa.isSelectedForDownload=false;
		}
		
		private function onScrollApplication(ev:ScrollEvent):void {
			scrollPosition = ev.position;
		}
		
		private function added():void {
		    if (searchInput) {
                if (searchInput.text=="Keyword, name, country...") {
                	searchInput.setStyle("fontStyle","italic");
                	searchInput.setStyle("color","#999999");
                	searchInput.setStyle("fontSize","12");
                } else {
                	searchInput.setStyle("fontStyle","normal");
                	searchInput.setStyle("color","#333333");
                	searchInput.setStyle("fontSize","13");
                }                	
            }
		}
		
        private function changeStyle():void {
/*          	if (search_text.text=="Keyword, name, country...") {
            	search_text.setStyle("fontStyle","normal");
            	search_text.setStyle("color","#333333");
            	search_text.setStyle("fontSize","13");
            	search_text.text = '';
            }else if (search_text.text==""){            	
            	search_text.setStyle("fontStyle","italic");
            	search_text.setStyle("color","#999999");
            	search_text.setStyle("fontSize","11");
            	search_text.text = 'Keyword, name, country...'; 
            } */
        }
        
        private function onMapReady(ev:MapEvent):void {
        	var zco:ZoomControlOptions= new ZoomControlOptions({position:new ControlPosition(ControlPosition.ANCHOR_TOP_LEFT, 10, 10)});
        	map.addEventListener(MapZoomEvent.ZOOM_CHANGED, deleteRectangle);
            map.addControl(new ZoomControl(zco));
            map.enableScrollWheelZoom();
            map.setCenter(new LatLng(0,0),1);
            //map.addControl(new MapTypeControl());
            map.setMapType(MapType.PHYSICAL_MAP_TYPE);
            
            //Add the PA layer
			tilesPane = map.getPaneManager().createPane();
			tl = new GeoserverTileLayer(map);
			tlo = new TileLayerOverlay(tl);

			tl.addEventListener("GEOSERVER_TILE_LAYER_LOADED",function(ev:Event):void {
				discretLoading.visible=false;
			});
			tl.addEventListener("GEOSERVER_TILE_LAYER_LOADING",function(ev:Event):void {
				discretLoading.visible=true;
			});
			tilesPane.addOverlay(tlo);	
			tlo.foreground.alpha=1;            
            
            //link when bbox of map changes
            map.addEventListener(MapZoomEvent.ZOOM_CHANGED,function(event:MapZoomEvent):void {
            	getPas(1);
            });
            map.addEventListener(MapMoveEvent.MOVE_END,function(event:MapMoveEvent):void {
            	getPas(1);
            });
            
            
        }	
        
	      private function doGeocode():void {
	        // Geocoding example
	        var geocoder:ClientGeocoder = new ClientGeocoder();
	        
	        geocoder.addEventListener(
	          GeocodingEvent.GEOCODING_SUCCESS,
	          function(event:GeocodingEvent):void {
	          var placemarks:Array = event.response.placemarks;
	          if (placemarks.length > 0) {
	          	var bbox:LatLngBounds =  new LatLngBounds(
	          		new LatLng(placemarks[0].ExtendedData.LatLonBox.north,placemarks[0].ExtendedData.LatLonBox.west),
	          		new LatLng(placemarks[0].ExtendedData.LatLonBox.south,placemarks[0].ExtendedData.LatLonBox.east));	
	          		
	            map.setCenter(placemarks[0].point,map.getBoundsZoomLevel(bbox));
	          }
	        });
	        geocoder.addEventListener(
	          GeocodingEvent.GEOCODING_FAILURE,
	          function(event:GeocodingEvent):void {
	            trace(event);
	            trace(event.status);
	          });
	        geocoder.geocode(searchInput.text);
	      }
        
        private function deleteRectangle (ev: MapZoomEvent):void {
        	try {
    			map.removeChild(selectionRectangle);
    		}catch (erObject:Error) { 
      			trace("There isn't rectangles");
      		}
        }
        
        private function onClickMapArea(ev:MapMouseEvent):void {
        	selectedArea = new ArrayCollection();
        	selectedArea.addItem(ev.latLng);
        }
        
        private function onMouseUpMapArea(ev:MapMouseEvent):void {
        	selectedArea.addItem(ev.latLng);
        	/* uckAppManager.rectTool(); */
        	dataBbox=new LatLngBounds();
			for each(var m:LatLng in selectedArea) {
				dataBbox.extend(m);
			}
			
			/* map.setCenter(dataBbox.getCenter(),map.getBoundsZoomLevel(dataBbox)); */
        	
        }
        
        private function checkSelectArea():void {
        	if (checkArea.selected) {
        		map.disableDragging();
				map.addEventListener(MouseEvent.MOUSE_DOWN, onMouseDown);
				map.addEventListener(MouseEvent.MOUSE_UP, onMouseUp);
				map.addEventListener(MouseEvent.MOUSE_MOVE, onMouseMove);
        		map.addEventListener(MapMouseEvent.MOUSE_DOWN,onClickMapArea);
        		map.addEventListener(MapMouseEvent.MOUSE_UP,onMouseUpMapArea);
        		map.addEventListener(MouseEvent.ROLL_OVER, onMouseOverMap);
        		map.addEventListener(MouseEvent.ROLL_OUT, onMouseOutMap);
        	} else {
        		map.enableDragging();
        		try {
        			map.removeChild(selectionRectangle);
        		}catch (erObject:Error) { 
          			trace("There isn't rectangles");
          		}
        		map.removeEventListener(MouseEvent.MOUSE_DOWN, onMouseDown);
				map.removeEventListener(MouseEvent.MOUSE_UP, onMouseUp);
				map.removeEventListener(MouseEvent.MOUSE_MOVE, onMouseMove);
        		map.removeEventListener(MapMouseEvent.MOUSE_DOWN,onClickMapArea);
        		map.removeEventListener(MapMouseEvent.MOUSE_UP,onMouseUpMapArea);
        		map.removeEventListener(MouseEvent.ROLL_OVER, onMouseOverMap);
        		map.removeEventListener(MouseEvent.ROLL_OUT, onMouseOutMap);
        	}
        }
        
        private function onMouseOverMap(ev:MouseEvent):void {
        	CursorManager.setCursor(Resource.CURSOR_RECTANGLE);
        }
        
        private function onMouseOutMap(ev:MouseEvent):void {
        	CursorManager.removeAllCursors();
        }
        

		private function onMouseDown(e:MouseEvent):void {
						
			map.addEventListener(MouseEvent.MOUSE_MOVE, onMouseMove);

			r = new Rectangle(Number(e.stageX-(((stage.width-874)/2)+25)),Number(e.stageY-287) + (scrollPosition),0,0);
			
			selectionRectangle.x = r.x;
			selectionRectangle.y = r.y;
			selectionRectangle.width = r.width;
			selectionRectangle.height = r.height;
			
			map.addChild(selectionRectangle);
		}
		
		private function onMouseUp(e:MouseEvent):void {
			map.removeEventListener(MouseEvent.MOUSE_MOVE, onMouseMove);
			/* map.removeChild(selectionRectangle); */
		}
		
		private function onMouseMove(e:MouseEvent):void {

			r.bottomRight = new Point(e.stageX-(((stage.width-874)/2)+25), e.stageY-287 + (scrollPosition));
			selectionRectangle.x = r.x;
			selectionRectangle.y = r.y;
			selectionRectangle.width = r.width;
			selectionRectangle.height = r.height;				
		}
		
		
		private function getPas(p:Number):void {
			currentState = "loading";	
			//jsonService.url = Application.application.serverUrl + "/regions/"+_regionId+"/countries?page="+p+"&per_page=24";
			var params:String = "";
			if (map!=null && map.isLoaded()) {
				var bbox:LatLngBounds = map.getLatLngBounds();
				params=bbox.getNorth()+"/"+bbox.getSouth()+"/"+bbox.getWest()+"/"+bbox.getEast()+"/"+p;
			} else {
				params="90/-90/-180/180/1";				
			}
			jsonService.url = "http://localhost/php/json.php/DumpServices.getPasByBoundingBox/"+params;
			jsonService.send();		
			page=p;
		}
		
		private function getSearchResult(event:ResultEvent):void {
				var res:Object = JSON.decode(event.result as String);
				var pasArr:Array = [];
				for(var i:String in res.results as Array)  {
					var pa:Pa = new Pa();
					pa.id=res.results[i].id;
					pa.name=res.results[i].name;
					pa.country=res.results[i].country;
					pasArr.push(pa);
					//res.results[i] = new ObjectProxy(res.results[i]);
				}
				areasArr=new ArrayCollection(pasArr);
				totalAreas=res.total;
				totalPages = Math.ceil(totalAreas/24);			
				currentState = "";
		}

        
	]]>
</mx:Script>
	<mx:Canvas width="100%" height="400" styleName="softBlueUp" backgroundSize="100%" id="canvas_map">
		<mx:Label text="Search and browse the WDPA throw the map"  fontFamily="Helvetica" fontStyle="normal" fontSize="12" fontWeight="bold" color="#666666" width="264" left="10" top="10"/>
		<mx:HBox left="10" right="10" top="34" horizontalAlign="right" verticalAlign="middle" horizontalGap="4">
			<mx:Label text="search"  fontFamily="Helvetica" fontSize="12" color="#666666" height="13"/>
			<mx:TextInput id="searchInput" 
				focusIn="changeStyle()" focusOut="changeStyle()"
				fontStyle="italic" paddingTop="5" paddingLeft="5" paddingRight="5" fontSize="11" 
				fontFamily="Helvetica" color="#999999" width="341" styleName="searchTextInput" 
				backgroundSize="100%" borderStyle="none" height="25" enter="doGeocode()"/>
			<mx:Button id="checkArea" width="29" styleName="boundingBoxButton" height="25" useHandCursor="true" 
				toggle="true" mouseChildren="false" buttonMode="true" click="checkSelectArea()"/>
		</mx:HBox>
		
		<maps:Map  left="10" bottom="10" right="10" id="map" height="326" key="ABQIAAAAsIunaSEq-72JsQD5i92_2RTb-vLQlFZmc2N8bgWI8YDPp5FEVBSSShDhDbHNKr3tTC6UaCKqvYjyOQ"
			mapevent_mapready="onMapReady(event)"/>
		<components:DiscretLoading id="discretLoading" left="39" top="73"/>
		<mx:Button x="546" y="35" styleName="ephimerButton" width="35" 
			click="doGeocode()" visible="{!searchInput.text==''}" 
			useHandCursor="true" buttonMode="true" mouseChildren="false"/>

	</mx:Canvas>
	
	<mx:Canvas width="100%" height="100%" id="canvas1">
		<mx:TileList dataProvider="{areasArr}"  width="100%" height="100%" itemRenderer="org.vizzuality.view.download.itemrenders.ProtectedAreaResultItem" 
			paddingLeft="0" paddingRight="0" paddingTop="0" paddingBottom="0" borderStyle="none">
		</mx:TileList>
	</mx:Canvas>
	
	<mx:Canvas width="100%" height="34" styleName="softBlueBottom" backgroundSize="100%">
		<mx:Label  right="70" verticalCenter="0" width="211" text="{'page '+page+' of '+totalPages}" textAlign="right" fontSize="11" fontFamily="Helvetica" color="#414141" fontWeight="bold"/>
		<mx:Button right="39" click="getPas(page-1)" enabled="{page>1}" verticalCenter="0" styleName="whitePaginationLeft" width="27" height="24" useHandCursor="true" mouseChildren="false" buttonMode="true"/>
		<mx:Button right="8"  click="getPas(page+1)" enabled="{page &lt; totalPages}" verticalCenter="0" styleName="whitePaginationRight" width="27" height="24" useHandCursor="true" mouseChildren="false" buttonMode="true"/>
	</mx:Canvas>	
	
	<mx:Canvas width="100%" height="66" borderStyle="solid" cornerRadius="4" styleName="ContainersInfo">	
		<mx:Image x="13" y="10" width="43" height="47" source="{resource.Resource.INFORMATION}"/>
		<mx:Label text="If you want to download a complete country “country based tab”"  left="58" right="7" top="19" fontFamily="Helvetica" fontWeight="bold" fontSize="13" color="#414141"/>
		<mx:HBox id="hbox1" x="58" y="34"  horizontalGap="0">
			<mx:Label text="Do you want to download a complete country data? Then"  fontSize="12" fontFamily="Helvetica" color="#414141"/>
			<components:LinkLabel text="go to the “country tab”"  fontSize="12" fontFamily="Helvetica" color="#336699" fontWeight="bold"
				default_color="0x336699" default_hover_color="0x003366" default_hover_decoration="none" 
				text_decoration="none" default_weight="bold" default_hover_weight="bold"
				click="{SWFAddress.setValue('/rec/checkout/countries/africa')}"/>
		</mx:HBox>
	</mx:Canvas>
	<mx:HTTPService id="jsonService" resultFormat="text" method="GET" result="getSearchResult(event)" fault="trace(event.message)" />
	
</mx:VBox>